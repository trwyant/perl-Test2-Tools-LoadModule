#!/usr/bin/env perl

use 5.008001;

use strict;
use warnings;

use Getopt::Long 2.33;
use Pod::Usage;
use Test2::V0;
use Test2::Tools::EventDumper;
use Test2::Tools::LoadModule;

our $VERSION = '0.000_009';

my %opt;

GetOptions( \%opt,
    qw{ diagnostic|skip=s@ import=s@ name=s version=s },
    help => sub { pod2usage( { -verbose => 2 } ) },
)
    and @ARGV == 2
    and $ARGV[0] =~ m/ \A [[:alpha:]] /smx
    and my $code = Test2::Tools::LoadModule->can( shift @ARGV )
    or pod2usage( { -verbose => 0 } );

my @arg = @ARGV;

push @arg, $opt{$_} for qw{ version import name diagnostic };
pop @arg while ! defined $arg[-1];
$arg[2]
    and @{ $arg[2] } = grep { $_ ne '[]' } @{ $arg[2] };

my $events = intercept {
    SKIP: {
	$code->( @arg );
    }
};

print dump_events $events;

__END__

=head1 TITLE

events - Execute load_module_*() and dump the generated events.

=head1 SYNOPSIS

 events load_module_ok Bad::Module -diag Fubar
 events -help
 events -version

=head1 OPTIONS

=head2 -diagnostic

 -diagnostic 'This is a diagnostic'

This option specifies a diagnostic for C<load_module_ok()> or
C<load_module_p_ok()>. It can be specified more than once.

=head2 -help

This option displays the documentation for this script. The script then
exits.

=head2 -import

This option specifies an explicit import. It can be specified more than
once. A value of C<'[]'> specifies an empty array reference as the
argument.

=head2 -name

This option specifies the name argument.

=head2 -skip

 -skip 17

This option specifies the number of skipped tests for
C<load_module_or_skip()> and C<load_module_p_or_skip()>.

=head2 -version

This option specifies a version number for the module.

=head1 DETAILS

This Perl script executes a
L<Test2::Tools::LoadModule|Test2::Tools::LoadModule> test subroutine and
dumps the events generated by it. B<Note> that test diagnostics will not
appear in the dump if they are attached as C<info>.

The command-line arguments are, in order:

=over

=item The subroutine to execute

This must be C<load_module_ok>, C<load_module_p_ok>,
C<load_module_or_skip>, C<load_module_p_or_skip>,
C<load_module_or_skip_all>, or C<load_module_p_or_skip_all>.

=item The name of the module to load

=back

The subsequent subroutine arguments are specified by options. In order,
they are

=over

=item L<-version|/-version>

=item L<-import|/-import>

=item L<-name|/-name>

=item L<-diagnostic|/-diagnostic> or L<-skip|/-skip>.

=back

=head1 AUTHOR

Thomas R. Wyant, III F<wyant at cpan dot org>

=head1 COPYRIGHT AND LICENSE

Copyright (C) 2020 by Thomas R. Wyant, III

This program is free software; you can redistribute it and/or modify it
under the same terms as Perl 5.10.0. For more details, see the full text
of the licenses in the directory LICENSES.

This program is distributed in the hope that it will be useful, but
without any warranty; without even the implied warranty of
merchantability or fitness for a particular purpose.

=cut

# ex: set textwidth=72 :
